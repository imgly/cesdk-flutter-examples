plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
    id 'com.github.gmazzo.buildconfig'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty("flutter.versionCode")
if (flutterVersionCode == null) {
    flutterVersionCode = "1"
}

def flutterVersionName = localProperties.getProperty("flutter.versionName")
if (flutterVersionName == null) {
    flutterVersionName = "1.0"
}

buildConfig {
    className "ShowcasesFlutterBuildConfig"
    packageName "ly.img.editor.flutter"
    buildConfigField String, "UNSPLASH_HOST", System.env.CESDK_AND_UNSPLASH_HOST ?: localProperties.getProperty("unsplash_host") ?: ""
}

android {
    namespace = "ly.img.editor.flutter.showcases"
    compileSdk = 35 // flutter.compileSdkVersion is 34, remove hardcode when flutter updates this value
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "ly.img.editor.flutter.showcases"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        // highlight-minsdk
        minSdk = 24
        // highlight-minsdk
        targetSdk = 35 // flutter.targetSdkVersion is 34, remove hardcode when flutter updates this value
        versionCode = flutterVersionCode.toInteger()
        versionName = flutterVersionName
    }

    signingConfigs {
        release {
            storeFile rootProject.file("android-release.keystore")
            storePassword System.getenv("CESDK_AND_KEYSTORE_PWD") ?: localProperties.getProperty("release_key_password")
            keyAlias "cesdk-dev"
            keyPassword System.getenv("CESDK_AND_KEYSTORE_PWD") ?: localProperties.getProperty("release_key_password")
        }
    }

    buildTypes {
        release {
            def shrinkAndMinifyEnabled = System.getenv("CESDK_SHRINK_AND_MINIFY_ENABLED") == null
              || System.getenv("CESDK_SHRINK_AND_MINIFY_ENABLED") == "true"
            signingConfig signingConfigs.release
            minifyEnabled shrinkAndMinifyEnabled
            shrinkResources shrinkAndMinifyEnabled
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
      compose true
    }

    composeOptions {
      kotlinCompilerExtensionVersion = "1.5.3"
    }
}

dependencies {
  implementation "ly.img:camera:1.60.0-rc.0"
  implementation "ly.img:editor:1.60.0-rc.0"
  implementation "androidx.compose:compose-bom:2023.05.01"
  implementation "androidx.activity:activity-compose:1.6.1"
}

flutter {
    source = "../.."
}
